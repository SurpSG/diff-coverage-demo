buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.form-com.diff-coverage-gradle:diff-coverage:0.9.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.form.diff-coverage'

    group 'org.example'
    version '1.0-SNAPSHOT'

    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        dependsOn test
        group = "Reporting"
        description = "Generate Jacoco coverage reports"

        executionData files("${buildDir}/jacoco/jacoco.exec")
        sourceDirectories = files("${projectDir}/src/main/java")

        def generatedFilter = [
                '**/generated/**',
        ]
        classDirectories = files(
                fileTree(dir: "${buildDir}/classes/java/main", excludes: generatedFilter),
                fileTree(dir: "${buildDir}/classes/main", excludes: generatedFilter)
        )

        reports {
            xml.enabled = true
            html.enabled = true
            csv.enabled = true
            xml.destination file("${buildDir}/reports/jacoco.xml")
            csv.destination file("${buildDir}/reports/jacoco.csv")
            html.destination file("${buildDir}/reports/fullCoverage")
        }

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: generatedFilter)
            })
        }
    }

    diffCoverageReport {
        diffSource {
            git.compareWith "refs/heads/master"
        }

        jacocoExecFiles = files(jacocoTestReport.executionData)
        classesDirs = files(jacocoTestReport.classDirectories)
        srcDirs = files(jacocoTestReport.sourceDirectories)

        reports {
            html = true
            csv = true
            xml = true
            baseReportDir = "${buildDir}/reports"
        }
    }

    diffCoverage {
        group = "Reporting"
        description = "Generate Jacoco incremential coverage reports"
        dependsOn jacocoTestReport
    }
}
